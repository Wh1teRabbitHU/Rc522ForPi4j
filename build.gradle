buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

group 'hu.whiterabbit'
version '0.0.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()

    maven {
        url "https://oss.sonatype.org/content/groups/public"
    }
}

ext {
    // Raspberry pi
    pi4jVersion = "1.2-SNAPSHOT"

    // Logging
    slf4jVersion = "1.7.25"
    logbackVersion = "1.2.3"

    // Test
    junitVersion = "4.12"

    defaultExampleClass = "ReadData"
}

dependencies {
    compile(
            // Raspberry pi
            "com.pi4j:pi4j-core:${pi4jVersion}",

            // Logging
            "org.slf4j:slf4j-api:${slf4jVersion}"
    )
    runtime(
            // Logging
            "org.slf4j:jcl-over-slf4j:${slf4jVersion}",
            "ch.qos.logback:logback-classic:${logbackVersion}"
    )
    testCompile(
            // Junit
            "junit:junit:${junitVersion}",
    )
}

jar {
    manifest {
        attributes('Main-Class': 'hu.whiterabbit.rc522forpi4j.example.' + (project.hasProperty('exampleClass') ? project.exampleClass : defaultExampleClass))
    }
}

shadowJar {
    baseName = "rc522forpi4j-example-" + (project.hasProperty('exampleClass') ? project.exampleClass : defaultExampleClass)
    destinationDir = file("examples")
    classifier = ""
    version = ""
}

wrapper {
    gradleVersion = '4.10'
}
